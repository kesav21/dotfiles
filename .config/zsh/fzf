#!/bin/zsh

export FZF_DEFAULT_COMMAND='echo I have disabled the default command feature'
export FZF_DEFAULT_OPTS="$FZF_DEFAULT_OPTS"' --reverse --height=99% --exit-0'
export FZF_DEFAULT_OPTS="$FZF_DEFAULT_OPTS"' --color=fg:#a89984,bg:#1d2021,hl:#83a598 --color=fg+:#ebdbb2,bg+:#1d2021,hl+:#83a598 --color=info:#afaf87,prompt:#cc241d,pointer:#d3869b --color=marker:#8ec07c,spinner:#1d2021,header:#83a598'

select-history() {
	fc -rl 1 |
		perl -ne 'print if !$seen{(/^\s*[0-9]+\**\s+(.*)/, $1)}++' |
		fzf +m -n2..,.. --tiebreak=index --bind=ctrl-r:toggle-sort
}

fzf-history() {
	setopt localoptions noglobsubst noposixbuiltins pipefail no_aliases 2> /dev/null
	local num="$(select-history)"
	local ret=$?
	[ -n "$num" ] && zle vi-fetch-history -n $num
	zle reset-prompt
	return $ret
}
zle     -N   fzf-history
bindkey '^R' fzf-history

fzf-select-file-relative() {
	setopt localoptions pipefail no_aliases 2> /dev/null
	LBUFFER="${LBUFFER}$(fd -H -tf ".*" . | fzf --select-1 | tr '\n' ' ')"
	local ret=$?
	zle reset-prompt
	return $ret
}
zle     -N   fzf-select-file-relative
bindkey '\es' fzf-select-file-relative

fzf-select-file-absolute() {
	setopt localoptions pipefail no_aliases 2> /dev/null
	LBUFFER="${LBUFFER}$(fd -H -tf ".*" ~ | sed "s|$HOME|~|g" | fzf --select-1 | sed "s|~|$HOME|g" | tr '\n' ' ')"
	local ret=$?
	zle reset-prompt
	return $ret
}
zle     -N   fzf-select-file-absolute
bindkey '\eS' fzf-select-file-absolute

fzf-select-dir-relative() {
	setopt localoptions pipefail no_aliases 2> /dev/null
	LBUFFER="${LBUFFER}$(fd -H -td ".*" . | fzf --select-1 | tr '\n' ' ')"
	local ret=$?
	zle reset-prompt
	return $ret
}
zle     -N   fzf-select-dir-relative
bindkey '\ec' fzf-select-dir-relative

fzf-select-dir-absolute() {
	setopt localoptions pipefail no_aliases 2> /dev/null
	LBUFFER="${LBUFFER}$(fd -H -td ".*" ~ | sed "s|$HOME|~|g" | fzf --select-1 | sed "s|~|$HOME|g" | tr '\n' ' ')"
	local ret=$?
	zle reset-prompt
	return $ret
}
zle     -N   fzf-select-dir-absolute
bindkey '\eC' fzf-select-dir-absolute

# TODO: handle special characters like single quotes and double quotes
rename-format() {
	[ -n "$1" ] && {
		maxlen="$(wc -L <<< "$1")"
		while read line; do
			padding="$((maxlen - ${#line} + 1))"
			printf "mv '%s'" "$line"
			[ "$padding" -gt 0 ] && printf ' %.0s' {1..$padding}
			printf "'%s'\n" "$line"
		done <<< "$1"
	}
}

fzf-rename() {
	setopt localoptions pipefail no_aliases 2> /dev/null
	LBUFFER="$(rename-format "$(fd -H -tf '.*' . | fzf -m --select-1)")"
	[ -n "$LBUFFER" ] && zle edit-command-line
	zle reset-prompt
}
zle     -N   fzf-rename
bindkey '^p' fzf-rename
